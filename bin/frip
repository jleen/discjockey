#!/usr/bin/env zsh

# Windows is based on a completely different scientific principle.
[[ `uname` == CYGWIN_NT* ]] && { wrip $* ; exit 0 }

[[ -f ~/.dj ]] && . ~/.dj

DWAIT=${DJ_WAIT:-${0%/*}/dwait}
DEJECT=${DJ_EJECT:-${0%/*}/deject}

PYTHON=${DJ_PYTHON:-python}
MUSIC=${DJ_MUSIC:-~/music}
CATALOG=${DJ_CATALOG:-~/catalog}

MORE_ARGS=()

find_mount() {
    mount \
        | grep -v auto \
        | grep $1 \
        | grep $2 \
        | head -1 \
        | cut -d' ' -f3
}

# The AFP logic is mostly here for the sake of Mac OS.
if [[ -n $DJ_AFP_CATALOG ]]; then
    host=`echo $DJ_AFP_CATALOG | cut -d: -f1`
    share=`echo $DJ_AFP_CATALOG | cut -d: -f2`
    dir=`echo $DJ_AFP_CATALOG | cut -d: -f3`
    mount=`find_mount $host $share`
    CATALOG=$mount/$dir
fi
if [[ -n $DJ_AFP_MUSIC ]]; then
    host=`echo $DJ_AFP_MUSIC | cut -d: -f1`
    share=`echo $DJ_AFP_MUSIC | cut -d: -f2`
    dir=`echo $DJ_AFP_MUSIC | cut -d: -f3`
    mount=`find_mount $host $share`
    MUSIC=$mount/$dir
fi
    
if [ "$1" = "-f" ]; then
    MORE_ARGS+=("--allow_wrong_length")
    shift
fi

if [[ $2 == "m3u" ]]; then
    MORE_ARGS+=("--rename")
elif [[ -n $2 ]]; then
    MORE_ARGS+=("--nocreate_playlists" "--first_disc" $2)
fi

$PYTHON ${0%/*}/../py/djrip.py \
    --album="$1" \
    --catalog="$CATALOG" \
    --music="$MUSIC" \
    --eject_cmd=$DEJECT \
    --wait_cmd=$DWAIT \
    --discid_cmd="cd-discid $CDROM" \
    --cdparanoia_bin=cdparanoia \
    --flac_bin=flac \
    $MORE_ARGS
